plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}


allprojects {
    group = 'com.ys'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
}

ext {
    set('springCloudVersion', "2023.0.0")
}

subprojects {
    bootJar.enabled = false
    jar.enabled = false
    compileJava.options.encoding = 'UTF-8'

    apply plugin: 'java-library' //하위 모듈이 상위 모듈의 라이브러리를 의존할 수 있게 하는 플러그인
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    test {
        useJUnitPlatform()
    }


    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }


    dependencies {
        if (isAppleSilicon()) {
            runtimeOnly("io.netty:netty-resolver-dns-native-macos:4.1.94.Final:osx-aarch_64")
        }

        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
        //  https://springdoc.org/ http://localhost:8080/swagger-ui/index.html

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.kafka:spring-kafka'
        testImplementation 'com.tngtech.archunit:archunit:1.0.1'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

}

tasks.named('test') {
    useJUnitPlatform()
}

boolean isAppleSilicon() {
    System.getProperty("os.name") == "Mac OS X" && System.getProperty("os.arch") == "aarch64"
}

bootRun {
    enabled = false
}

bootJar {
    enabled = false
}

bootBuildImage{
    enabled = false
}